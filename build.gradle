group 'at.jku.ssw'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 8
targetCompatibility = 8

ext {
    javassistVersion = '3.23.1-GA'
    junitVersion = '5.3.1'
    commonsCliVersion = '1.4'
    hamcrestVersion = '1.3'
    log4jVersion = '2.11.1'
}

wrapper {
    gradleVersion = '4.10'
}

mainClassName = 'at.jku.ssw.java.bytecode.generator.jb_generator.JBGenerator'


jar {
    manifest {
        attributes 'Implementation-Title': 'Java Bytecode Generator',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    baseName = 'jbgenerator'
    archiveName = "${jar.baseName}.${jar.extension}"
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

run {
    classpath = sourceSets.main.runtimeClasspath
}


repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.javassist', name: 'javassist', version: javassistVersion
    compile group: 'commons-cli', name: 'commons-cli', version: commonsCliVersion

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion

    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrestVersion
}
